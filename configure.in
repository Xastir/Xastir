dnl Process this file with autoconf to produce a configure script.
dnl Currently maintained by Chuck Byam (KG4IJB), cbyam@virginia.edu
#
# $Header$
#
# Xastir Configure Script
#
# Report Xastir configure bugs via
#   bugs@xastir.org
#

AC_INIT(acinclude.m4)


XASTIR_MSG_CONFIGURE_START

# Set version
MAJOR_VERSION=1
MINOR_VERSION=1
MICRO_VERSION=1

# quess the system
AC_CANONICAL_SYSTEM

AC_SUBST(MAJOR_VERSION)dnl
AC_SUBST(MINOR_VERSION)dnl
AC_SUBST(MICRO_VERSION)dnl

AM_INIT_AUTOMAKE(xastir,$MAJOR_VERSION.$MINOR_VERSION.$MICRO_VERSION)

echo ""
echo "configuring $PACKAGE $MAJOR_VERSION.$MINOR_VERSION.$MICRO_VERSION"
echo ""


# Ensure that make can run correctly
AM_SANITY_CHECK

# Generate configure header.
AM_CONFIG_HEADER(config.h)

# set some default values based on system
# Thanks to Jack Twilley for the port idea
case $host in
   *-*-freebsd* )
      AC_DEFINE(XASTIR_SYSTEM, "FreeBSD")
      ac_tnc_port='"/dev/cuaa0"'
      ac_gps_port='"/dev/cuaa1"'
      AC_MSG_WARN(*** FreeBSD support is not fully implemented yet! ***)
      ;;
   *-*-*sunos* )
      AC_DEFINE(XASTIR_SYSTEM, "SunOS")
      ac_tnc_port='"/dev/cua/a"'
      ac_gps_port='"/dev/cua/b"'
      AC_MSG_WARN(*** SunOS support is not fully implemented yet! ***)
      ;;
   *-*-*solaris* )
      AC_DEFINE(XASTIR_SYSTEM, "Solaris")
      AC_DEFINE_UNQUOTED(USING_SOLARIS, 1)
      LIBS="${LIBS} -lposix4 -lpthread"
      ac_tnc_port='"/dev/cua/a"'
      ac_gps_port='"/dev/cua/b"'
      AC_MSG_WARN(*** Solaris support is not fully implemented yet! ***)
      ;;
   *-*-*linux* )
      AC_DEFINE(XASTIR_SYSTEM, "Linux")
      ac_tnc_port='"/dev/ttyS0"'
      ac_gps_port='"/dev/ttyS1"'
      ;;
    *-*apple-darwin* )
      AC_DEFINE_UNQUOTED(USING_MAC_OS_X, 1)
      AC_DEFINE(XASTIR_SYSTEM, "Darwin")
      ac_tnc_port='"/dev/cua/a"'
      ac_gps_port='"/dev/cua/b"'
      AC_MSG_WARN(*** Mac OS X support is not fully implemented yet! ***)
      ;;
   * )
      AC_MSG_WARN(Host type is not recognized)
      ac_tnc_port='""'
      ac_gps_port='""'
      ;;
esac

AC_DEFINE_UNQUOTED(TNC_PORT, $ac_tnc_port, [Define default TNC port.])
AC_DEFINE_UNQUOTED(GPS_PORT, $ac_gps_port, [Define default GPS port.])

# Check for programs
#AC_CHECK_COMPILERS
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_LN_S
AM_MAINTAINER_MODE
AC_PROG_YACC

if test "$GCC" = "yes";then
  case $host in
    *-*-freebsd* )
      CFLAGS="$CFLAGS -Wall"
      ;;
    *-*-*sunos* )
      CFLAGS="$CFLAGS -Wall"
      ;;
    *-*-*solaris* )
      ;;
    *-*-*linux* )
      CFLAGS="$CFLAGS -Wall"
      ;;
    *-*apple-darwin* )
      CFLAGS="$CFLAGS -Wall"
      ;;
    * )
      CFLAGS="$CFLAGS -Wall"
      ;;
  esac
fi

dnl Checks for libraries
XASTIR_CHECK_LIBPTHREAD
AC_CHECK_LIBM

AC_CHECK_FUNCS(socket strdup strstr atexit getaddrinfo)
AC_CHECK_FUNCS(snprintf vsnprintf)
#AC_CHECK_LIB(socket, socket)
#AC_CHECK_LIB(inet, socket))

AC_CHECK_FUNC(inet_addr,,
AC_CHECK_LIB(nsl,inet_addr))
AC_FUNC_GETPGRP

XASTIR_CHECK_AX25
XASTIR_PATH_X
XASTIR_CHECK_MOTIF
AC_FIND_XPM

dnl Go back and check for Xp
dnl This one is driving me nuts! Force it for now
LIBS="$LIBS $XPM_LIBS"
AC_CHECK_LIB(Xp, XpGetDocumentData)
AC_CHECK_LIB(Xpm, XpmWriteFileFromPixmap)

XASTIR_CHECK_GEOTIFF_LIBS
XASTIR_CHECK_FESTIVAL
AC_FUNC_GETPGRP

XASTIR_CHECK_SHAPELIB_LIBS
XASTIR_CHECK_IMAGEMAGICK_LIBS

AC_HEADER_STDC
AC_TYPE_SIZE_T
AC_TYPE_PID_T
AC_TYPE_SIGNAL
XASTIR_TYPE_SOCKLEN_T
AC_FUNC_STRFTIME

AC_CHECK_HEADERS(fcntl.h sys/fcntl.h unistd.h sys/time.h sys/select.h sys/socket.h)

AC_HEADER_TIME
AC_FUNC_VPRINTF
AC_DEFINE(_GNU_SOURCE, 1, [This is GNU compliant source, use GNU extensions.])
dnl AC_DEFINE(_REENTRANT, 1, [Use reentrant code if available.])

AC_FUNC_SELECT_ARGTYPES
AC_REPLACE_FUNCS(getopt_long)

AC_DEFINE_UNQUOTED(STIPPLE, 1) dnl Use crowbar and lets keep going
to_call=APX$MAJOR_VERSION$MINOR_VERSION$MICRO_VERSION
AC_SUBST(to_call)
AC_DEFINE_UNQUOTED(XASTIR_TOCALL, "$to_call")

dnl Set XASTIR_DATA_BASE in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(XASTIR_DATA_BASE, "${ac_default_prefix}/xastir")
else
  AC_DEFINE_UNQUOTED(XASTIR_DATA_BASE, "${prefix}/xastir")
fi

XASTIR_CHECK_CCPIPE
AC_PATH_PROG(xastirpath, xastir)
XASTIR_PRUNE_DUPLICATES

AC_OUTPUT([Makefile \
    src/Makefile \
    m4/Makefile \
    config/Makefile \
    help/Makefile \
    symbols/Makefile \
    callpass/Makefile \
    xastir.spec])

echo
echo $PACKAGE $VERSION

echo
echo Building with AX25.......... : $has_axlib
echo Building with GeoTiff....... : $use_geotiff
echo Building with ShapeLib...... : $use_shapelib
echo Building with ImageMagick... : $use_imagemagick
echo Building with Festival...... : $use_festival
echo
echo
echo Xastir will be installed in $prefix/bin.
if test "x$xastirpath" != "x" ; then
        echo Warning: You have an old copy of Xastir at $xastirpath.
fi

XASTIR_MSG_CONFIGURE_END
